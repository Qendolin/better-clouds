plugins {
	id "fabric-loom"
	id "com.modrinth.minotaur"
}

loom {
	accessWidenerPath = project.rootProject.file("src/main/resources/betterclouds.accesswidener")
	runConfigs.configureEach {
		ideConfigGenerated true
		runDir "../../run"
	}
}

base {
	archivesName = project.archives_base_name
}

var mcVersion = stonecutter.current.version
version = project.mod_version
if(project.version_type != "release") {
	version += "-" + project.version_type
}
version += "+" + mcVersion
if(project.version_type != "release") {
	version += ".rev." + 'git rev-parse --short HEAD'.execute().text.trim()
}
group = project.maven_group

repositories {
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/releases/"
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven { url "https://maven.shedaniel.me/" }
	maven {
		name 'Xander Maven'
		url 'https://maven.isxander.dev/releases'
	}
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // for com.twelvemonkeys.imageio
	maven { url "https://maven.quiltmc.org/repository/release" } // for org.quiltmc.parsers
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:$mcVersion"
	mappings "net.fabricmc:yarn:${property('yarn_mappings')}:v2"

	modImplementation "net.fabricmc:fabric-loader:${property('loader_version')}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${property('fabric_version')}"
	modImplementation "com.terraformersmc:modmenu:${property('modmenu_version')}"
	modImplementation "dev.isxander:yet-another-config-lib:${property('yacl_version')}-fabric"
	compileOnly project.rootProject.files("libraries/DistantHorizonsApi-3.0.0-nightly-377f7d23.jar")

	modCompileOnly "maven.modrinth:sodium-extra:${property('sodium_extra_version')}"
	modCompileOnly "maven.modrinth:iris:${property('iris_version')}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}

	exclude("assets/**/*.ase")
	exclude("assets/**/*.xcf")
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = findProperty('java_version').toString().toInteger()
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archives_base_name}"}
	}
}

modrinth  { // Make sure it runs after build!
	def secrets = new Properties()

	project.rootProject.file("secrets.properties").withInputStream {
		stream -> secrets.load(stream)
	}


	token.set(secrets.MODRINTH as String)
	projectId = '5srFLIaK'
	versionNumber = "${property('mod_version')}+${mcVersion}-${property('version_type').substring(0, 1)}" // Will fail if Modrinth has this version already
	versionName = "${property('mod_version')} for ${mcVersion} ${property('version_type')}"
	versionType = "${property('version_type')}"
	// On fabric, use 'remapJar' instead of 'jar'
	uploadFile = remapJar  // This is the java jar task. If it can't find the jar, try 'jar.outputs.getFiles().asPath' in place of 'jar'
	gameVersions.add(mcVersion)
	loaders.add('fabric')
	dependencies { // A special DSL for creating dependencies
		// scope.type
		// The scope can be `required`, `optional`, `incompatible`, or `embedded`
		// The type can either be `project` or `version`
		required.project "fabric-api"
		required.version "yacl", "${property('yacl_version')}-fabric"
		optional.project "sodium"
		optional.project "iris"
		optional.project "modmenu"
		incompatible.project "vulkanmod"
	}
}
