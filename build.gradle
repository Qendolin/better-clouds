plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
}

loom {
	accessWidenerPath = file("src/main/resources/betterclouds.accesswidener")
}

base {
	archivesName = project.archives_base_name
}

version = project.mod_version
if(project.version_type != "release") {
	version += "-" + project.version_type
}
version += "+" + project.minecraft_version
if(project.version_type != "release") {
	version += ".rev." + 'git rev-parse --short HEAD'.execute().text.trim()
}
group = project.maven_group

repositories {
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/releases/"
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven { url "https://maven.shedaniel.me/" }
	maven {
		name 'Xander Maven'
		url 'https://maven.isxander.dev/releases'
	}
	maven { url "https://oss.sonatype.org/content/repositories/snapshots" } // for com.twelvemonkeys.imageio
	maven { url "https://maven.quiltmc.org/repository/release" } // for org.quiltmc.parsers
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
	modImplementation("dev.isxander:yet-another-config-lib:${project.yacl_version}-fabric")
	compileOnly files("libraries/DistantHorizonsApi-3.0.0-nightly-377f7d23.jar")

	modCompileOnly "maven.modrinth:sodium-extra:${project.sodium_extra_version}"
	modCompileOnly "maven.modrinth:iris:${project.iris_version}"
	modCompileOnly "maven.modrinth:voxy:0.1.5-alpha"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}

	exclude("assets/**/*.ase")
	exclude("assets/**/*.xcf")
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.20.5 upwards uses Java 21.
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

modrinth  { // Make sure it runs after build!
	def secrets = new Properties()

	file("./secrets.properties").withInputStream {
		stream -> secrets.load(stream)
	}

	token.set(secrets.MODRINTH as String)
	projectId = '5srFLIaK'
	versionNumber = "${project.mod_version}+${project.minecraft_version}-${project.version_type.substring(0, 1)}" // Will fail if Modrinth has this version already
	versionName = "${project.mod_version} for ${project.minecraft_version} ${project.version_type}"
	versionType = "${project.version_type}"
	// On fabric, use 'remapJar' instead of 'jar'
	uploadFile = remapJar  // This is the java jar task. If it can't find the jar, try 'jar.outputs.getFiles().asPath' in place of 'jar'
	gameVersions.add("${project.minecraft_version}")
	loaders.add('fabric')
	dependencies { // A special DSL for creating dependencies
		// scope.type
		// The scope can be `required`, `optional`, `incompatible`, or `embedded`
		// The type can either be `project` or `version`
		required.project "fabric-api"
		required.version "yacl", "${project.yacl_version}-fabric"
		optional.project "sodium"
		optional.project "iris"
		optional.project "modmenu"
		incompatible.project "vulkanmod"
	}
}
